plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
    id 'application'
    id 'java'
    id 'jacoco'
    id 'de.jansauer.printcoverage' version '2.0.0'
    id 'com.adarshr.test-logger' version '3.2.0'
}

group 'com.finx'
version '1.0'

mainClassName = 'com.finx.dropwizard.DropwizardApplication'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation platform('io.dropwizard:dropwizard-dependencies:2.1.3')
    implementation platform('ru.vyarus.guicey:guicey-bom:5.6.1-1')
    implementation platform('org.testcontainers:testcontainers-bom:1.17.5')

    implementation project(':common')

    implementation 'io.dropwizard:dropwizard-core'
    implementation 'io.dropwizard:dropwizard-client'
    implementation 'io.dropwizard:dropwizard-migrations'
    implementation 'ru.vyarus:dropwizard-guicey'
    implementation 'ru.vyarus.guicey:guicey-jdbi3'

    runtimeOnly 'org.postgresql:postgresql:42.5.0'

    testImplementation('io.dropwizard:dropwizard-testing') {
        exclude group: 'junit', module: 'junit'
    }
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.junit.jupiter:junit-jupiter-params')
    testImplementation('org.mockito:mockito-core')
    testImplementation('org.mockito:mockito-junit-jupiter')
    testImplementation('org.assertj:assertj-core')
    testImplementation("org.testcontainers:postgresql")
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Main-Class': mainClassName
    }
    archiveName 'dropwizard-example.jar'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

testSets {
    integrationTest {
        useJUnitPlatform()
        mustRunAfter test
        finalizedBy jacocoTestReport
    }
}

tasks.named('check') {
    dependsOn(integrationTest, printCoverage)
}


testlogger {
    theme 'mocha'
}

jacocoTestReport {
    reports.xml.enabled true
    reports.html.enabled true
}
